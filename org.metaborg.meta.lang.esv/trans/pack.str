module pack

imports
  
  include/EditorService
  editor-common.generated

strategies

  pack-main-descriptor-file: (_, _, esv, path, project-path) -> None()
  where
    <is-main-descriptor-file> path
  with
    packed-esv := <create-packed-descriptor(<read-definitions> esv)> esv;
    name := <get-language-name> packed-esv;
    filename := $[[project-path]/include/[name].packed.esv];
    <WriteToTextFile> (filename, packed-esv)

  pack-main-descriptor-file: (_, _, _, path, _) -> None()
  where
    <not(is-main-descriptor-file)> path

  is-main-descriptor-file = string-ends-with(|".main.esv")

  create-packed-descriptor(definitions) = Module(!$[[<id>].packed],id,definitions)

strategies

  get-language-name = get-language-option(?LanguageName(<id>))

  get-language-option(s): Module(_,_,definition*) -> option
  where
    option := <filter(?Language(_,<id>));concat;getfirst(s)> definition*
    
strategies

  read-definitions: Module(name,Imports(import*),definition*) -> [definition*,import-definition*]
  with
    	name' := <strip-annos> name;
    	rules(IsImported: name');
      new-import* := <filter(?Import(<strip-annos>);not(IsImported))> import*;
      import-definition* := <map(read-descriptor-file;read-definitions);concat> new-import*

  read-definitions: Module(name,NoImports(),definition*) -> definition*
  where
    	name' := <strip-annos> name;
    	rules(IsImported: name')

  read-descriptor-file: name -> esv
  with
    esv := <find-descriptor-file;parse-file>

  find-descriptor-file: name -> file
  with
    file := <resolve-in-path(|<editorservice-source-and-include-locations>)> $[[name].esv]
  <+
    <debug(!"Error: Cannot find ESV module ");fail> name
